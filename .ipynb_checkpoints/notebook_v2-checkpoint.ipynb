{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "09f433d0",
   "metadata": {},
   "source": [
    "# Sujet 2: Modélisation des interdépendances\n",
    "\n",
    "**Quel est l’impact d’une baisse de la capacité de LNG sur le système en général et en particulier sur\n",
    "le système électrique ?**\n",
    "\n",
    "Pour aller plus loin : modéliser le stockage gaz\n",
    "\n",
    "**Authors**: Lucas DEFRANCE, Quentin DEHAENE, Jules PAINDESSOUS--POETE"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aae960d0",
   "metadata": {},
   "source": [
    "## Imports"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6edcd196",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Necessary imports\n",
    "using JuMP\n",
    "using XLSX\n",
    "using DataFrames\n",
    "#use the solver you want\n",
    "using HiGHS\n",
    "using CSV"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d0ea0e60",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define global variables\n",
    "data_file_zootopia = \"data/TP_empilement.xlsx\";\n",
    "data_file = \"data/Donnees_etude_de_cas_ETE305.xlsx\";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "254b073e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Extract data for power mix\n",
    "data_ranges = [\"A2:A24\", \"D2:D24\", \"E2:E24\", \"F2:F24\", \"G2:G24\", \"H2:H24\"]\n",
    "column_names = [:plant_name, :nb_units, :pmax_per_unit, :pmin_per_unit, :dmin, :costs]\n",
    "data_types = [String, Int, Float64, Float64, Int, Float64]\n",
    "\n",
    "# Read data from file\n",
    "df = DataFrame()\n",
    "for (col_name, range, data_type) in zip(column_names, data_ranges, data_types)\n",
    "    df[!, col_name] = data_type.(vec(XLSX.readdata(data_file, \"Parc électrique\", range)))\n",
    "end\n",
    "\n",
    "# Generate the plant_type vector using array comprehension\n",
    "plant_type = [\n",
    "    i in [14, 15] ? \"thermal_fatal\" :\n",
    "    i == 18 ? \"hydro_fatal\" :\n",
    "    i == 19 ? \"hydro_lake\" :\n",
    "    i == 20 ? \"hydro_step\" :\n",
    "    i in [21, 22] ? \"wind\" :\n",
    "    i == 23 ? \"solar\" :\n",
    "    \"thermal\" for i in 1:size(df, 1)\n",
    "]\n",
    "\n",
    "# Append the vector as a new column to the DataFrame\n",
    "df[!, :plant_type] = plant_type\n",
    "\n",
    "println(df)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "65487cc3",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Initialize arrays to store values for non-fatal thermal clusters\n",
    "names_th, costs_th, Pmin_th, Pmax_th, dmin_th = [], [], [], [], []\n",
    "\n",
    "# Filter the DataFrame based on the plant_type column\n",
    "df_thermal = filter(row -> row.plant_type == \"thermal\", df)\n",
    "\n",
    "# Loop through each row of the DataFrame\n",
    "for row in eachrow(df_thermal)\n",
    "    # Iterate over each plant name and unit number in the row\n",
    "    for i in 1:row.nb_units\n",
    "        # Construct the complete plant name with unit number and append it to names_th\n",
    "        push!(names_th, \"$(row.plant_name)-$i\")\n",
    "        \n",
    "        # Append other values to respective arrays\n",
    "        push!(costs_th, row.costs)\n",
    "        push!(Pmin_th, row.pmin_per_unit)\n",
    "        push!(Pmax_th, row.pmax_per_unit)\n",
    "        push!(dmin_th, row.dmin)\n",
    "    end\n",
    "end\n",
    "\n",
    "# Convert the vectors to 2D arrays\n",
    "costs_th, Pmin_th, Pmax_th, dmin_th = hcat(costs_th), hcat(Pmin_th), hcat(Pmax_th), hcat(dmin_th)\n",
    "\n",
    "# Create a dictionary to map indices to plant names\n",
    "Nth = length(costs_th)\n",
    "dict_th = Dict(i => names_th[i] for i in 1:length(names_th))\n",
    "\n",
    "# Check the value associated with index 1 in dict_th\n",
    "dict_th[1]\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b8a69444",
   "metadata": {},
   "outputs": [],
   "source": [
    "#data for hydro reservoir\n",
    "Nhy = 1 #number of hydro generation units\n",
    "Pmin_hy = zeros(Nhy);\n",
    "Pmax_hy = df[19,:pmax_per_unit] *ones(Nhy); #MW\n",
    "e_hy = XLSX.readdata(data_file, \"Détails historique hydro\", \"B2:B13\");\n",
    "# Calculate the mean stock level\n",
    "weekly_mean_e_hy = repeat([sum(e_hy) / 52], Nhy)\n",
    "costs_hy = df[19,:costs]*ones(Nhy); #MWh\n",
    "\n",
    "#data for STEP\n",
    "Pmax_STEP = df[20,:pmax_per_unit] #MW\n",
    "rSTEP = 0.75\n",
    "max_stock_STEP = 201600; #MWh"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "736a6727",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We choose to use run 52 simulations of 1 week each\n",
    "Tp = 24*7"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d980d657",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "nb_week_year = 52\n",
    "UC_continu, stock_STEP_continu, UP_continu, DO_continu = [], [], [], []\n",
    "for w in 1:nb_week_year\n",
    "    # We start the simulation at Tmin and end it at Tmax\n",
    "    Tmin = w*24*7\n",
    "    Tmax = Tmin + 24*7-1; #optimization by one week intervals\n",
    "    Tp = 24*7;\n",
    "    \n",
    "    #data for load and fatal generation\n",
    "    load = XLSX.readdata(data_file_zootopia, \"TP\", \"C$(10+Tmin):C$(Tmax + 10)\");\n",
    "    wind = XLSX.readdata(data_file_zootopia, \"TP\", \"D$(10+Tmin):D$(Tmax + 10)\");\n",
    "    solar = XLSX.readdata(data_file_zootopia, \"TP\", \"E$(10+Tmin):E$(Tmax + 10)\");\n",
    "    hydro_fatal = XLSX.readdata(data_file_zootopia, \"TP\", \"F$(10+Tmin):F$(Tmax + 10)\");\n",
    "    thermal_fatal = XLSX.readdata(data_file_zootopia, \"TP\", \"G$(10+Tmin):G$(Tmax + 10)\");\n",
    "    #total of RES\n",
    "    Pres = wind + solar + hydro_fatal + thermal_fatal;\n",
    "    \n",
    "    #costs\n",
    "    cth = repeat(costs_th', Tp) #cost of thermal generation €/MWh\n",
    "    chy = repeat(costs_hy', Tp) #cost of hydro generation €/MWh\n",
    "    cuns = 5000*ones(Tp) #cost of unsupplied energy €/MWh\n",
    "    cexc = 0*ones(Tp); #cost of in excess energy €/MWh\n",
    "\n",
    "\n",
    "    #############################\n",
    "    #create the optimization model\n",
    "    #############################\n",
    "    model = Model(HiGHS.Optimizer)\n",
    "\n",
    "    #############################\n",
    "    #define the variables\n",
    "    #############################\n",
    "    #thermal generation variables\n",
    "    \n",
    "    @variable(model, Pth[1:Tp,1:Nth] >= 0)\n",
    "    @variable(model, UCth[1:Tp,1:Nth], Bin)\n",
    "    @variable(model, UPth[1:Tp,1:Nth], Bin)\n",
    "    @variable(model, DOth[1:Tp,1:Nth], Bin)\n",
    "    #hydro generation variables\n",
    "    @variable(model, Phy[1:Tp,1:Nhy] >= 0)\n",
    "    #unsupplied energy variables\n",
    "    @variable(model, Puns[1:Tp] >= 0)\n",
    "    #in excess energy variables\n",
    "    @variable(model, Pexc[1:Tp] >= 0)\n",
    "    #weekly STEP variables\n",
    "    @variable(model, Pcharge_STEP[1:Tp] >= 0)\n",
    "    @variable(model, Pdecharge_STEP[1:Tp] >= 0)\n",
    "    @variable(model, stock_STEP[1:Tp] >= 0)\n",
    "\n",
    "    #############################\n",
    "    #define the objective function\n",
    "    #############################\n",
    "    @objective(model, Min, sum(Pth.*cth)+sum(Phy.*chy)+Puns'cuns+Pexc'cexc)\n",
    "\n",
    "\n",
    "    #############################\n",
    "    #define the constraints\n",
    "    #############################\n",
    "    #balance constraint\n",
    "    @constraint(model, balance[t in 1:Tp], sum(Pth[t,g] for g in 1:Nth) + sum(Phy[t,h] for h in 1:Nhy) + Pres[t] + Puns[t] - load[t] - Pexc[t] - Pcharge_STEP[t] + Pdecharge_STEP[t]== 0)\n",
    "    #thermal unit Pmax constraints\n",
    "    @constraint(model, max_th[t in 1:Tp, g in 1:Nth], Pth[t,g] <= Pmax_th[g]*UCth[t,g])\n",
    "    #thermal unit Pmin constraints\n",
    "    @constraint(model, min_th[t in 1:Tp, g in 1:Nth], Pmin_th[g]*UCth[t,g] <= Pth[t,g])\n",
    "\n",
    "\n",
    "    #thermal unit Dmin constraints\n",
    "    for g in 1:Nth\n",
    "        if (dmin_th[g] > 1)\n",
    "            @constraint(model, [t in 2:Tp], UCth[t,g]-UCth[t-1,g]==UPth[t,g]-DOth[t,g],  base_name = \"fct_th_$g\")\n",
    "            @constraint(model, [t in 1:Tp], UPth[t]+DOth[t]<=1,  base_name = \"UPDOth_$g\")\n",
    "            if (w==1)\n",
    "                @constraint(model, UPth[1,g]==0,  base_name = \"iniUPth_$g\")\n",
    "                @constraint(model, DOth[1,g]==0,  base_name = \"iniDOth_$g\")\n",
    "            else\n",
    "                @constraint(model, UCth[1,g]-UC_continu[g]==UPth[1,g]-DOth[1,g],  base_name = \"fct_th_0$g\")\n",
    "            end   \n",
    "            @constraint(model, [t in dmin_th[g]:Tp], UCth[t,g] >= sum(UPth[i,g] for i in (t-dmin_th[g]+1):t),  base_name = \"dminUPth_$g\")\n",
    "            @constraint(model, [t in dmin_th[g]:Tp], UCth[t,g] <= 1 - sum(DOth[i,g] for i in (t-dmin_th[g]+1):t),  base_name = \"dminDOth_$g\")\n",
    "            if (w==1)\n",
    "                @constraint(model, [t in 1:dmin_th[g]-1], UCth[t,g] >= sum(UPth[i,g] for i in 1:t), base_name = \"dminUPth_$(g)_init\")\n",
    "                @constraint(model, [t in 1:dmin_th[g]-1], UCth[t,g] <= 1-sum(DOth[i,g] for i in 1:t), base_name = \"dminDOth_$(g)_init\")\n",
    "\n",
    "            else\n",
    "                @constraint(model, [t in 1:dmin_th[g]-1], UCth[t,g] >= sum(UPth[i,g] for i in 1:t)+sum(UP_continu[j,g] for j in 25 + t - dmin_th[g]:25), base_name = \"dminUPth_$(g)_init_cont\")\n",
    "                @constraint(model, [t in 1:dmin_th[g]-1], UCth[t,g] <= 1-sum(DOth[i,g] for i in 1:t)-sum(DO_continu[j,g] for j in 25 + t - dmin_th[g]:25), base_name = \"dminDOth_$(g)_init_cont\")\n",
    "            end\n",
    "        end\n",
    "    end \n",
    "\n",
    "    \n",
    "    #hydro unit constraints\n",
    "    @constraint(model, unit_bounds_hy[t in 1:Tp, h in 1:Nhy], Pmin_hy[h] <= Phy[t,h] <= Pmax_hy[h])\n",
    "    #hydro stock constraint\n",
    "    @constraint(model, stock_bounds_hy[h in 1:Nhy], sum(Phy[t,h] for t in 1:Tp) <= weekly_mean_e_hy[h] )\n",
    "\n",
    "    #weekly STEP\n",
    "    @constraint(model, max_Pcharge_STEP[t in 1:Tp],Pcharge_STEP[t] <= Pmax_STEP)\n",
    "    @constraint(model, max_Pdecharge_STEP[t in 1:Tp],Pdecharge_STEP[t] <= Pmax_STEP)\n",
    "\n",
    "    @constraint(model, stock_STEP_max[t in 1:Tp], stock_STEP[t] <= max_stock_STEP)\n",
    "    @constraint(model, stock_STEP_evo[t in 2:Tp],stock_STEP[t] == stock_STEP[t-1] + Pcharge_STEP[t]*rSTEP - Pdecharge_STEP[t])\n",
    "    if (w==1)\n",
    "        @constraint(model,stock_STEP[1] == 0.5*max_stock_STEP)\n",
    "    else\n",
    "        @constraint(model,stock_STEP[1] == stock_STEP_continu + Pcharge_STEP[1]*rSTEP - Pdecharge_STEP[1])\n",
    "    end\n",
    "\n",
    "    #no need to print the model when it is too big\n",
    "    #solve the model\n",
    "    optimize!(model)\n",
    "    \n",
    "    # Collecting values for each iteration\n",
    "    UC_continu= value.(UCth[Tp, 1:Nth])\n",
    "    stock_STEP_continu= value(stock_STEP[Tp])\n",
    "    UP_continu= value.(UPth[Tp-24:Tp, 1:Nth])\n",
    "    DO_continu= value.(DOth[Tp-24:Tp, 1:Nth])\n",
    "\n",
    "    \n",
    "    #------------------------------\n",
    "    #Results\n",
    "    println(\"semaine :\",w)\n",
    "    @show termination_status(model)\n",
    "    @show objective_value(model)\n",
    "\n",
    "    #exports results as csv file\n",
    "    th_gen = value.(Pth)\n",
    "    hy_gen = value.(Phy)\n",
    "    STEP_charge = value.(Pcharge_STEP)\n",
    "    STEP_decharge = value.(Pdecharge_STEP)\n",
    "\n",
    "    # new file created\n",
    "    file_name = string(\"results/resultsW\",w,\".csv\")\n",
    "    touch(file_name)\n",
    "\n",
    "    # file handling in write mode\n",
    "    f = open(file_name, \"w\")\n",
    "\n",
    "    for i in 1:length(dict_th)\n",
    "        plant_name = dict_th[i]\n",
    "        write(f, string(plant_name, \",\"))\n",
    "    end\n",
    "    write(f, \"Hydro, STEP pompage, STEP turbinage, RES, load, Net load \\n\")\n",
    "\n",
    "    for t in 1:Tp\n",
    "        for g in 1:Nth\n",
    "            write(f, \"$(th_gen[t,g]), \")\n",
    "        end\n",
    "        for h in 1:Nhy\n",
    "            write(f, \"$(hy_gen[t,h]),\")\n",
    "        end\n",
    "    write(f, \"$(STEP_charge[t]), $(STEP_decharge[t]),\")\n",
    "    write(f, \"$(Pres[t]),  $(load[t]), $(load[t]-Pres[t]) \\n\")\n",
    "\n",
    "    end\n",
    "\n",
    "    close(f)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0166da00",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CSV\n",
    "using DataFrames\n",
    "\n",
    "# File paths\n",
    "file_paths = []\n",
    "for i in 1:51\n",
    "    file_name = \"results/resultsW$i.csv\"\n",
    "    push!(file_paths, file_name)\n",
    "end\n",
    "\n",
    "# Initialize an empty DataFrame to store combined data\n",
    "combined_df = DataFrame()\n",
    "\n",
    "# Loop through each file path\n",
    "for file_path in file_paths\n",
    "    # Read the CSV file\n",
    "    df = CSV.read(file_path, DataFrame)\n",
    "    \n",
    "    # If it's not the first file, skip the header\n",
    "    if nrow(combined_df) > 0\n",
    "        df = df[2:end, :]\n",
    "    end\n",
    "    \n",
    "    # Append the data to the combined DataFrame\n",
    "    append!(combined_df, df)\n",
    "end\n",
    "\n",
    "# Write the combined DataFrame to a new CSV file\n",
    "CSV.write(\"results/combined_results.csv\", combined_df)\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "72eecb63",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4289a388",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
